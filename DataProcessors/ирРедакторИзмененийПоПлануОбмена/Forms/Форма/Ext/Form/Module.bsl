Перем мМакетныйОбъект;
Перем мТекущаяГруппаТипаМетаданных;
Перем мСоответствиеСтрокДереваИМетаданных;
Перем мВыборкаРезультата;
Перем мРезультатЗапроса;
Перем мВыбранныеУзлы;
Перем мМетаданныеПланаОбмена;
Перем мДанныеПоКоличествуИзменений;
Перем мВнутреннееИмяТаблицы;

Функция ПолучитьКоличествоЗарегистрированныхОбъектовДляМетаданногоПоУзлу(МетаданныеОбъекта)
	
	Запрос = Новый Запрос();
	//ИмяТаблицыДляПоискаЗарегистрированных = ПолучитьПоМетаданномуНазвание(МетаданныеОбъекта);
	//Если ПустаяСтрока(ИмяТаблицыДляПоискаЗарегистрированных) Тогда
	//	Возврат 0;
	//КонецЕсли;
	ИмяТаблицыДляПоискаЗарегистрированных = ИмяТаблицыДляПоискаЗарегистрированных + "." + МетаданныеОбъекта.Имя + ".Изменения";
	Запрос.Текст = "
	   |ВЫБРАТЬ 
	   |    Количество(*) КАК КоличествоЗарегистрированных,
   	   |    СУММА(ВЫБОР КОГДА РегистрацияИзменений.НомерСообщения ЕСТЬ NULL ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоНевыгруженных,
	   |    СУММА(ВЫБОР КОГДА РегистрацияИзменений.НомерСообщения ЕСТЬ NULL ТОГДА 0 ИНАЧЕ 1 КОНЕЦ) КАК КоличествоВыгруженных
	   |ИЗ
	   |	" + ИмяТаблицыДляПоискаЗарегистрированных + " КАК РегистрацияИзменений
	   |ГДЕ
	   |РегистрацияИзменений.Узел В (&Узлы)";
	Запрос.УстановитьПараметр("Узлы", мВыбранныеУзлы);
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Если ТаблицаДанных.Количество() = 0 Тогда
		СтрокаДанных = ТаблицаДанных.Добавить();
	Иначе		
		СтрокаДанных = ТаблицаДанных[0];
	КонецЕсли;
	Возврат СтрокаДанных;
	
КонецФункции

Процедура ОбновитьКоличествоДляСтрокиДерева(СтрокаДерева = Неопределено)
	
	Если СтрокаДерева = Неопределено Тогда
		СтрокаДерева = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока;
	КонецЕсли; 
	КолонкиКоличества = Новый Массив();
	КолонкиКоличества.Добавить("КоличествоЗарегистрированных");
	КолонкиКоличества.Добавить("КоличествоВыгруженных");
	КолонкиКоличества.Добавить("КоличествоНевыгруженных");
	Для Каждого ИмяКолонкиКоличества Из КолонкиКоличества Цикл
		СтароеКоличество = СтрокаДерева[ИмяКолонкиКоличества];
		СтрокаДерева[ИмяКолонкиКоличества] = 0;
		Для Каждого ЭлементПакета Из мДанныеПоКоличествуИзменений Цикл
			СтрокиРезультата = ЭлементПакета.НайтиСтроки(Новый Структура("ПолноеИмя", СтрокаДерева.ПолноеИмя));
			Для Каждого СтрокаРезультата Из СтрокиРезультата Цикл
				Если мВыбранныеУзлы.Найти(СтрокаРезультата.Узел) = Неопределено Тогда
					Продолжить;
				КонецЕсли; 
				СтрокаДерева[ИмяКолонкиКоличества] = СтрокаДерева[ИмяКолонкиКоличества] + СтрокаРезультата[ИмяКолонкиКоличества];
			КонецЦикла;
		КонецЦикла;
		Родитель = СтрокаДерева.Родитель;
		Пока Родитель <> Неопределено Цикл
			Родитель[ИмяКолонкиКоличества] = Родитель[ИмяКолонкиКоличества] - СтароеКоличество + СтрокаДерева[ИмяКолонкиКоличества];
			Родитель = Родитель.Родитель;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПостроитьДеревоОбменаДанных(СоставПланаОбмена, МетаданныеДляОграничения = Неопределено)
	
	Если МетаданныеДляОграничения = Неопределено Тогда
		СоставПланаОбмена.Строки.Очистить();
		мСоответствиеСтрокДереваИМетаданных.Очистить();
		Если мМетаданныеПланаОбмена = Неопределено Тогда
			Возврат;
		КонецЕсли; 
		СтрокиМетаОбъектов = ирКэш.Получить().ТаблицаТиповМетаОбъектов.НайтиСтроки(Новый Структура("Категория", 0));
		Для Каждого ТипМетаОбъектов Из СтрокиМетаОбъектов Цикл
			Единственное = ТипМетаОбъектов.Единственное;
			Если Ложь
				Или Единственное = "КритерийОтбора"
				Или Единственное = "ЖурналДокументов"
				Или Единственное = "ВнешнийИсточникДанных"
			Тогда
				Продолжить;
			КонецЕсли;
			СтрокаДерева = СоставПланаОбмена.Строки.Добавить();
			СтрокаДерева.Имя = ТипМетаОбъектов.Единственное;
			СтрокаДерева.Представление = ирОбщий.ПолучитьПредставлениеИзИдентификатораЛкс(ТипМетаОбъектов.Множественное);
			СтрокаДерева.ПолноеИмя = ТипМетаОбъектов.Единственное;
			СтруктураДанных = Новый Структура();
			СтруктураДанных.Вставить("ОписаниеТипа", ТипМетаОбъектов);
			СтруктураДанных.Вставить("СтрокаДерева", СтрокаДерева);
			мСоответствиеСтрокДереваИМетаданных[ТипМетаОбъектов.Единственное] = СтруктураДанных;
		КонецЦикла;
		МетаданныеОбъекта = Неопределено;
		ТаблицаВсехТаблиц = ирКэш.ПолучитьТаблицуВсехТаблицБДЛкс();
		Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(мМетаданныеПланаОбмена.Состав.Количество(), "Подготовка данных");
		Для Каждого ЭлементСостава Из мМетаданныеПланаОбмена.Состав Цикл
			ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
			МетаданныеОбъектаПредка = МетаданныеОбъекта;
			МетаданныеОбъекта = ЭлементСостава.Метаданные;
			Если МетаданныеОбъекта = Неопределено Тогда
				// Это сама конфигурация
				Продолжить;
			КонецЕсли;
			Если Истина
				И ОтображатьТолькоТаблицыСАвторегистрацией
				И ЭлементСостава.Авторегистрация <> АвтоРегистрацияИзменений.Разрешить
			Тогда
				Продолжить;
			КонецЕсли; 
			ПолноеИмяМД = МетаданныеОбъекта.ПолноеИмя();
			ИмяТаблицыБД = ирОбщий.ПолучитьИмяТаблицыИзМетаданныхЛкс(ПолноеИмяМД, , Ложь);
			ОписаниеТаблицы = ТаблицаВсехТаблиц.Найти(НРег(ИмяТаблицыБД), "НПолноеИмя");
			Если ИмяТаблицыБД = "Константы" Тогда
				// Для 8.2.13 и ниже
				ИмяТаблицыБД = ПолноеИмяМД;
				ОписаниеТаблицы = Новый Структура("Имя, Представление, Тип", МетаданныеОбъекта.Имя, МетаданныеОбъекта.Представление(), "Константа");
			КонецЕсли; 
			ЭлементСоответствия = мСоответствиеСтрокДереваИМетаданных[ОписаниеТаблицы.Тип];
			Если ЭлементСоответствия = Неопределено Тогда
				//СообщитьОшибкуИис("Невозможно определить тип объекта: " + Строка(МетаданныеОбъекта));
				Продолжить;
			КонецЕсли;
			СтрокаДерева = ЭлементСоответствия.СтрокаДерева;
			СтрокаДобавления = СтрокаДерева.Строки.Добавить(); 
			СтрокаДобавления.Имя = ОписаниеТаблицы.Имя;
			СтрокаДобавления.Представление = ОписаниеТаблицы.Представление;
			СтрокаДобавления.ПолноеИмя = ИмяТаблицыБД;
			СтрокаДобавления.РежимАвтоРегистрации = (ЭлементСостава.АвтоРегистрация = АвтоРегистрацияИзменений.Разрешить);
			СтрокаДобавления.КоличествоЗарегистрированных = 0;
		КонецЦикла;
		ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
		СтрокиКУдалению = Новый Массив;
		Для Каждого СтрокаТипаМетаданных Из СоставПланаОбмена.Строки Цикл
			Если СтрокаТипаМетаданных.Строки.Количество() = 0 Тогда
				СтрокиКУдалению.Добавить(СтрокаТипаМетаданных);
			КонецЕсли; 
		КонецЦикла;
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			СоставПланаОбмена.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла;
	Иначе
		ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.Строки.Очистить();
		Если мМетаданныеПланаОбмена = Неопределено Тогда
			Возврат;
		КонецЕсли; 
	КонецЕсли;

	Если ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения Тогда
		ОбновитьСтатистикуВСоставеПланаОбмена();
		СтрокиКУдалению = Новый Массив();
		Для Каждого СтрокаДерева1 Из СоставПланаОбмена.Строки Цикл
			Для Каждого СтрокаДерева2 Из СтрокаДерева1.Строки Цикл
				Если Истина
					И ОтображатьТолькоМетаданныеСИзменениями 
					И СтрокаДерева2.КоличествоЗарегистрированных = 0
				Тогда
					СтрокиКУдалению.Добавить(СтрокаДерева2);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			СтрокаКУдалению.Родитель.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла;
		
		СтрокиКУдалению = Новый Массив();
		Для Каждого СтрокаДерева1 Из СоставПланаОбмена.Строки Цикл
			Если СтрокаДерева1.Строки.Количество() = 0 Тогда
				СтрокиКУдалению.Добавить(СтрокаДерева1);
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			СоставПланаОбмена.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;
	    	
КонецПроцедуры

Функция ОбновитьСтатистикуВСоставеПланаОбмена()

	Если мДанныеПоКоличествуИзменений = Неопределено Тогда
		мДанныеПоКоличествуИзменений = ирОбщий.ВычислитьКоличествоСтрокТаблицВДеревеМетаданныхЛкс(СоставПланаОбмена, "ПолноеИмя", "КоличествоЗарегистрированных", Истина, 
			//Новый Структура("Узел", Узлы.ВыгрузитьКолонку("Ссылка"))
			Новый Структура("_ТипУзла_", Тип("ПланОбменаСсылка." + ИмяПланОбмена)));
	КонецЕсли; 
	ОбновитьИтогиПоУзлам();
	ирОбщий.ЗаполнитьКоличествоСтрокТаблицВДеревеМетаданныхЛкс(СоставПланаОбмена, мДанныеПоКоличествуИзменений, "ПолноеИмя", "КоличествоЗарегистрированных, КоличествоНевыгруженных, КоличествоВыгруженных",
		Новый Структура("Узел", мВыбранныеУзлы));
	Возврат Неопределено;

КонецФункции

Процедура ОбновитьИтогиПоУзлам()

	Для Каждого СтрокаУзла Из Узлы Цикл
		СтрокаУзла.КоличествоЗарегистрированных = 0;
		СтрокаУзла.КоличествоНевыгруженных = 0;
		СтрокаУзла.КоличествоВыгруженных = 0;
	КонецЦикла; 
	Если мДанныеПоКоличествуИзменений <> Неопределено Тогда
		Для Каждого ТаблицаРезультата Из мДанныеПоКоличествуИзменений Цикл
			Для Каждого СтрокаРезультата Из ТаблицаРезультата Цикл
				СтрокаУзла = Узлы.Найти(СтрокаРезультата.Узел, "Ссылка");
				Если СтрокаУзла = Неопределено Тогда
					СтрокаУзла = Узлы.Добавить();
					СтрокаУзла.Ссылка = СтрокаРезультата.Узел;
				КонецЕсли; 
				СтрокаУзла.КоличествоЗарегистрированных = СтрокаУзла.КоличествоЗарегистрированных + СтрокаРезультата.КоличествоЗарегистрированных;
				СтрокаУзла.КоличествоНевыгруженных = СтрокаУзла.КоличествоНевыгруженных + СтрокаРезультата.КоличествоНевыгруженных;
				СтрокаУзла.КоличествоВыгруженных = СтрокаУзла.КоличествоВыгруженных + СтрокаРезультата.КоличествоВыгруженных;
			КонецЦикла;
		КонецЦикла; 
	КонецЕсли; 

КонецПроцедуры

//Функция по имени родителя предка формируется префикс типа
Функция СформироватьПрефиксТипаСсылки(Знач НаименованиеПредка)
	
	Если НаименованиеПредка = "Справочники" Тогда
		Возврат "СправочникСсылка";
	ИначеЕсли НаименованиеПредка = "Документы" Тогда
		Возврат "ДокументСсылка";
	ИначеЕсли НаименованиеПредка = "ПланыВидовХарактеристик" Тогда
		Возврат "ПланВидовХарактеристикСсылка";
	ИначеЕсли НаименованиеПредка = "ПланыСчетов" Тогда
		Возврат "ПланСчетовСсылка";
	ИначеЕсли НаименованиеПредка = "ПланыВидовРасчета" Тогда
		Возврат "ПланВидовРасчетаСсылка";
	ИначеЕсли НаименованиеПредка = "БизнесПроцессы" Тогда
		Возврат "БизнесПроцессСсылка";
	ИначеЕсли НаименованиеПредка = "Задачи" Тогда
		Возврат "ЗадачаСсылка";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

//процедура строит дерево измененных объектов для узла
Процедура ПостроитьДеревоСоставаПланаОбмена(Знач МетаданныеДляОграничения = Неопределено)
	
	КлючТекущейСтроки = "";
	Если ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока <> Неопределено Тогда
		КлючТекущейСтроки = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока.ПолноеИмя;
	КонецЕсли; 
	КлючиВыделенныхСтрок = Новый Массив();
	Для Каждого ВыбраннаяСтрока Из ЭлементыФормы.СоставПланаОбмена.ВыделенныеСтроки Цикл
		Если ВыбраннаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		КлючиВыделенныхСтрок.Добавить(ВыбраннаяСтрока.ПолноеИмя);
	КонецЦикла;
	СоставПланаОбмена.Строки.Очистить();
	Если ТипЗнч(МетаданныеДляОграничения) = Тип("Строка") Тогда
		МетаданныеДляОграничения = Метаданные.НайтиПоПолномуИмени(МетаданныеДляОграничения);
	КонецЕсли; 
	// надо построить дерево тех метаданных которые участвуют в обмене
	ПостроитьДеревоОбменаДанных(СоставПланаОбмена, МетаданныеДляОграничения);
	
	Если МетаданныеДляОграничения = Неопределено Тогда
		СоставПланаОбмена.Строки.Сортировать("Имя", Истина);
	КонецЕсли;
	Если КлючТекущейСтроки <> "" Тогда
		ТекущаяСтрока = СоставПланаОбмена.Строки.Найти(КлючТекущейСтроки, "ПолноеИмя", Истина);
		Если ТекущаяСтрока <> Неопределено Тогда
			ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока = ТекущаяСтрока;
		КонецЕсли; 
	КонецЕсли; 
	Для Каждого КлючВыделеннойСтроки Из КлючиВыделенныхСтрок Цикл
		СтрокаДерева = СоставПланаОбмена.Строки.Найти(КлючВыделеннойСтроки, "ПолноеИмя", Истина);
		Если СтрокаДерева <> Неопределено Тогда
			ЭлементыФормы.СоставПланаОбмена.ВыделенныеСтроки.Добавить(СтрокаДерева);
		КонецЕсли; 
	КонецЦикла;
	Если ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока = Неопределено Тогда
		КП_ТаблицаИзмененийОбновить();
	КонецЕсли; 
	
КонецПроцедуры

//процедура добавляет/удаляет регистрацию для объектов
Процедура ДобавитьУдалитьРегистрациюДляОбъектов(Знач НовоеЗначение = Истина)
	
	КоличествоВыбранныхОбъектовМД = ЭлементыФормы.СоставПланаОбмена.ВыделенныеСтроки.Количество();
	Если КоличествоВыбранныхОбъектовМД = 0 Тогда
		Возврат;
	КонецЕсли; 
	РегистрацияВсехИзменений = Истина;
	НужноОбновлятьДерево = НовоеЗначение;
	СтрокаДобавленияУдаленияРегистрации = ?(НовоеЗначение, "Добавить", "Удалить");
	ВыбранныеСтрокиДерева = ПолучитьВыбранныеСтрокиДерева();
	Если КоличествоВыбранныхОбъектовМД = 1 Тогда
		УровеньВыбраннойСтроки = ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.Уровень();
		СписокРедактирования = Неопределено;
		Если УровеньВыбраннойСтроки = 0 Тогда
			// общий класс, справочники, константы...
			ТекстПодтверждения = СтрокаДобавленияУдаленияРегистрации + " регистрацию для всех объектов с типом: " + ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.Имя;
			ОтветПользователя = Вопрос(ТекстПодтверждения, РежимДиалогаВопрос.ДаНет);
			Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли;
		ИначеЕсли УровеньВыбраннойСтроки = 1 Тогда
			// конкретный справочник или константа
			Если ирОбщий.СтрокиРавныЛкс(ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.Родитель.ПолноеИмя, "Константы") Тогда
				РегистрацияВсехИзменений = Истина;
			Иначе
				ТекстПодтверждения = СтрокаДобавленияУдаленияРегистрации + " регистрацию для всех (Да) или выборочных (Нет) объектов с типом: " + ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.Имя;
				ОтветПользователя = Вопрос(ТекстПодтверждения, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет);
				Если ОтветПользователя = КодВозвратаДиалога.Отмена Тогда
					Возврат;
				ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Да Тогда
					РегистрацияВсехИзменений = Истина;
				ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Нет Тогда
					РегистрацияВсехИзменений = Ложь;
					СписокРедактирования = ПолучитьДанныеДляРегистрацииИзменений(ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.ПолноеИмя);
					Если СписокРедактирования = Неопределено Тогда
						Возврат;
					КонецЕсли;
					НужноОбновлятьДерево = Истина;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
	Иначе
		ТекстПодтверждения = СтрокаДобавленияУдаленияРегистрации + " регистрацию для всех объектов всех (" + ВыбранныеСтрокиДерева.Количество() + ") выделенных типов?";
		ОтветПользователя = Вопрос(ТекстПодтверждения, РежимДиалогаВопрос.ДаНет);
		Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если НовоеЗначение Тогда
		Комментарий = "Добавление";
	Иначе
		Комментарий = "Удаление";
	КонецЕсли; 
	Комментарий = Комментарий + " регистрации выбранных типов данных. " + ПредставлениеВыбранныхУзлов;
	ЗаписьЖурналаРегистрации("Изменение регистрации данных", УровеньЖурналаРегистрации.Предупреждение, мМетаданныеПланаОбмена,, Комментарий);
	Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(ВыбранныеСтрокиДерева.Количество());
	Для Каждого СтрокаДерева Из ВыбранныеСтрокиДерева Цикл
		ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
		Если СтрокаДерева = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		Если РегистрацияВсехИзменений Тогда
			ИзменитьРегистрациюЦелойТаблицы(СтрокаДерева, НовоеЗначение);
		Иначе
			// регистрация того, что выберет пользователь
			СтрокаОбновления = ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные;
			//Если СтрокаОбновления.Уровень() >= 2 Тогда
			//	СтрокаОбновления = СтрокаОбновления.Родитель;
			//КонецЕсли;
			Для Каждого ЗначенияСписка Из СписокРедактирования Цикл
				Успех = ирОбщий.ИзменитьРегистрациюДляУзлаЛкс(мВыбранныеУзлы, ЗначенияСписка.Значение, НовоеЗначение);
				//Если Истина
				//	И Успех 
				//	И Не ДобавитьРегистрацию 
				//	И Не НужноОбновлятьДерево 
				//Тогда
				//	ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.Родитель.Строки.Удалить(ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные);
				//КонецЕсли;
			КонецЦикла;
			ОбновитьИтогиПоТаблице(СтрокаДерева);
		КонецЕсли; 
	КонецЦикла;
	ирОбщий.ОсвободитьИндикаторПроцессаЛкс(Индикатор);
	Если НужноОбновлятьДерево Тогда
		ПостроитьДеревоСоставаПланаОбмена();
	Иначе
		КП_ТаблицаИзмененийОбновить();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьВыбранныеСтрокиДерева(СтрокиДерева = Неопределено, Результат = Неопределено)
	
	Если Результат = Неопределено Тогда
		Результат = Новый Массив;
	КонецЕсли; 
	Если СтрокиДерева = Неопределено Тогда
		СтрокиДерева = ЭлементыФормы.СоставПланаОбмена.ВыделенныеСтроки;
	КонецЕсли; 
	Для Каждого ВыбраннаяСтрока Из СтрокиДерева Цикл
		Если ВыбраннаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		УровеньВыбраннойСтроки = ВыбраннаяСтрока.Уровень();
		Если УровеньВыбраннойСтроки = 0 Тогда
			ПолучитьВыбранныеСтрокиДерева(ВыбраннаяСтрока.Строки, Результат);
		Иначе
			Если Результат.Найти(ВыбраннаяСтрока) = Неопределено Тогда
				Результат.Добавить(ВыбраннаяСтрока);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПолныеИменаВыделенныхТаблиц(СтрокиДерева = Неопределено, Результат = Неопределено)
	
	ВсеВыделенныеСтроки = ПолучитьВыбранныеСтрокиДерева();
	Результат = Новый СписокЗначений;
	Для Каждого ВыделеннаяСтрока Из ВсеВыделенныеСтроки Цикл
		Результат.Добавить(ВыделеннаяСтрока.ПолноеИмя);
	КонецЦикла;
	Результат.СортироватьПоЗначению();
	Возврат Результат;
	
КонецФункции

Функция ИзменитьРегистрациюЦелойТаблицы(СтрокаДерева, ДобавитьРегистрацию)

	ОбъектМеданных = ирОбщий.НайтиОбъектМетаДанныхПоПолномуИмениТаблицыБДЛкс(СтрокаДерева.ПолноеИмя);
	Успех = ирОбщий.ИзменитьРегистрациюДляУзлаЛкс(мВыбранныеУзлы, ОбъектМеданных, ДобавитьРегистрацию);
	//Если Успех И Не ДобавитьРегистрацию Тогда
	//	УдалитьКонечныеЭлементыДерева(ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные);
	//КонецЕсли;
	ОбновитьИтогиПоТаблице(СтрокаДерева);
	Возврат Неопределено;

КонецФункции

// для узла обмена показывает зарегистрированные изменения
Процедура КоманднаяПанельДереваОбъектовОбновить(Кнопка)
	
	мДанныеПоКоличествуИзменений = Неопределено;
	ПостроитьДеревоСоставаПланаОбмена();
		
КонецПроцедуры

Функция ПолучитьИндексКартинкиПоМетаданным(УровеньДерева, ДанныеСтроки)
	
	Если УровеньДерева = 0 Тогда
		СтрокаСоответствия = мСоответствиеСтрокДереваИМетаданных[ДанныеСтроки.Имя];
		Возврат СтрокаСоответствия.ОписаниеТипа.ИндексКартинкиМножественное;
	ИначеЕсли УровеньДерева = 1 Тогда
		СтрокаСоответствия = мСоответствиеСтрокДереваИМетаданных[ДанныеСтроки.Родитель.Имя];
		Возврат СтрокаСоответствия.ОписаниеТипа.ИндексКартинкиЕдинственное;
	КонецЕсли;
	
КонецФункции

// при выводе строки дерева
Процедура ДеревоОбменаПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Имя.ОтображатьКартинку = истина;
	ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ОтображатьКартинку = Ложь;
	ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "";
	УровеньДерева = ДанныеСтроки.Уровень();
	ОформлениеСтроки.Ячейки.Имя.ИндексКартинки = ПолучитьИндексКартинкиПоМетаданным(УровеньДерева, ДанныеСтроки);
	Если УровеньДерева = 1 Тогда
		ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ОтображатьКартинку = Истина;
		Если ДанныеСтроки.РежимАвтоРегистрации = Истина Тогда
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ИндексКартинки = 0;
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "Разрешить";
		Иначе
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ИндексКартинки = 1;
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "Запретить";
		КонецЕсли;
		ОформлениеСтроки.Ячейки.Имя.ИндексКартинки = ПолучитьИндексКартинкиПоМетаданным(УровеньДерева, ДанныеСтроки);
		Если Истина
			И ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения
			И ДанныеСтроки.КоличествоЗарегистрированных <> 0 
		Тогда
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст;
		КонецЕсли;
	ИначеЕсли УровеньДерева = 0 Тогда
		//Если ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения
		//	И ДанныеСтроки.КоличествоЗарегистрированных <> 0 Тогда
		//	ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "Зарегистрировано: " + Строка(ДанныеСтроки.КоличествоЗарегистрированных);	
		//КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// развернуть дерево
Процедура КоманднаяПанельДереваОбъектовРазвернуть(Кнопка)
	
	Если ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.СоставПланаОбмена.Развернуть(ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные, Истина);
	
КонецПроцедуры

// свернуть дерево
Процедура КоманднаяПанельДереваОбъектовСвернутьДерево(Кнопка)

	Если ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.СоставПланаОбмена.Свернуть(ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные);
	
КонецПроцедуры

// выбор строки дерева
Процедура ДеревоОбменаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УровеньВыбраннойСтроки = ВыбраннаяСтрока.Уровень();
	
	Если УровеньВыбраннойСтроки < 1 Тогда
		Элемент.Развернуть(ВыбраннаяСтрока);
		Возврат;
	КонецЕсли;
	Если УровеньВыбраннойСтроки = 1 Тогда
		ОбновитьИтогиПоТаблице();
	КонецЕсли;
	
КонецПроцедуры

//Процедура удаляет все строки дерева у которых уровень не меньше 2
Процедура УдалитьКонечныеЭлементыДерева(ДеревоДанных)
	
	Если ДеревоДанных.Уровень() >= 1 Тогда
		
		ДеревоДанных.Строки.Очистить();
		
	Иначе
		
		Для Каждого СтрокаДерева Из ДеревоДанных.Строки Цикл
			
			УдалитьКонечныеЭлементыДерева(СтрокаДерева);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// удаление регистрации изменений
Процедура КоманднаяПанельДереваОбъектовОтменитьРегистрациюИзменения(Кнопка)
	
	ДобавитьУдалитьРегистрациюДляОбъектов(Ложь);
		
КонецПроцедуры

//Функция возвращает данные для регистрации изменений
Функция ПолучитьДанныеДляРегистрацииИзменений(ПолноеИмяМД)
	
	// не для всех типов позволяем список объектов для регистрации редактировать
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		ФормаРедактированияОбъектов = ПолучитьФорму("ФормаВыбораОбъектовДляРегистрации", ЭтаФорма);
		МассивТипов = Новый Массив();
		МассивТипов.Добавить(Тип(СтрЗаменить(ПолноеИмяМД, ".", "Ссылка.")));
		ФормаРедактированияОбъектов.ОписаниеТиповРедактированияСписка = Новый ОписаниеТипов(МассивТипов);
		РезультатОткрытия = ФормаРедактированияОбъектов.ОткрытьМодально();
		Если РезультатОткрытия <> Истина Тогда
			Возврат Неопределено;
		КонецЕсли;
		СписокРедактирования = ФормаРедактированияОбъектов.СписокРедактирования;
		
		// Избавляемся от пустых ссылок, т.к. они приводят к глобальной регистрации всех данных
		НачальноеКоличество = СписокРедактирования.Количество(); 
		Для СчетчикСписокРедактирования = 1 По НачальноеКоличество Цикл
			ЭлементСписка = СписокРедактирования[НачальноеКоличество - СчетчикСписокРедактирования];
			Если Не ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
				СписокРедактирования.Удалить(ЭлементСписка);
			КонецЕсли;
		КонецЦикла;
		
		// ничего не выбрали для регистрации
		Если СписокРедактирования.Количество() = 0 Тогда 
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;	

	Возврат СписокРедактирования;
	
КонецФункции

// добавить регистрацию
Процедура КоманднаяПанельДереваОбъектовДобавитьРегистрацию(Кнопка)
	
	ДобавитьУдалитьРегистрациюДляОбъектов(Истина);
	
КонецПроцедуры

//Функция регистрирует изменнения для всего дерева объектов
Функция ИзменитьРегистрациюИзмененийНаВыбранныхУзлахДляВсегоСоставаПланаОбмена(НовоеЗначение = Истина)
	
	Если мВыбранныеУзлы.Количество() = 0 Тогда
		Предупреждение("Сначала необходимо выбрать узлы");
		Возврат Ложь;
	КонецЕсли; 
	ФормаВопроса = ПолучитьФорму("ФормаВыбораОбновленияРегистрации", ЭтаФорма);
	Если НовоеЗначение Тогда
		ФормаВопроса.ТекстВопросаПользователю = "Операция регистрации всех изменений необратима, Вы уверены, "
			+ "что хотите зарегистрировать изменения для выбранных узлов для ВСЕХ объектов, типы которых входят в план обмена?";
		Комментарий = "Добавление";
	Иначе
		ФормаВопроса.ТекстВопросаПользователю = "Операция удаления регистрации изменений необратима, Вы уверены, "
			+ "что хотите удалить регистрацию изменений для выбранных узлов для ВСЕХ объектов, типы которых входят в план обмена?";
		Комментарий = "Удаление";
	КонецЕсли; 
	РезультатОткрытия = ФормаВопроса.ОткрытьМодально();
	Если РезультатОткрытия <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	мДанныеПоКоличествуИзменений = Неопределено;
	ТолькоДляОбъектовСАвтоматическойРегистрацией = ФормаВопроса.ОбновлятьТолькоДляЭлементовСАвтоРегистрацией;
	Если ТолькоДляОбъектовСАвтоматическойРегистрацией Тогда
		Комментарий = Комментарий + " регистрации всех типов данных";
	Иначе
		Комментарий = Комментарий + " регистрации всех авторегистрируемых типов данных";
	КонецЕсли; 
	Комментарий = Комментарий + ". " + ПредставлениеВыбранныхУзлов;
	ЗаписьЖурналаРегистрации("Изменение регистрации данных", УровеньЖурналаРегистрации.Предупреждение, мМетаданныеПланаОбмена,, Комментарий);
	Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(мМетаданныеПланаОбмена.Состав.Количество(), "Регистрация изменений");
	Для Каждого ЭлементСостава Из мМетаданныеПланаОбмена.Состав Цикл
		ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
		ОбъектМеданных = ЭлементСостава.Метаданные;
		Если ОбъектМеданных = Неопределено Тогда
			// Это сама конфигурация
			Продолжить;
		КонецЕсли;
		Если Истина
			И ТолькоДляОбъектовСАвтоматическойРегистрацией
			И ЭлементСостава.АвтоРегистрация = АвтоРегистрацияИзменений.Запретить 
		Тогда
			Продолжить;
		КонецЕсли;
		Если НовоеЗначение Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(мВыбранныеУзлы, ОбъектМеданных);
		Иначе
			ПланыОбмена.УдалитьРегистрациюИзменений(мВыбранныеУзлы, ОбъектМеданных);
		КонецЕсли; 
	КонецЦикла;
	ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
	Возврат Истина;
	
КонецФункции

// Похожа на ПолучитьКлючСтрокиТаблицыБДИзСтрокиТаблицыЗначенийЛкс
Функция ПолучитьОбъектДанныхПоСтрокеИзменений(СтрокаДанных = Неопределено)
	
	Если СтрокаДанных = Неопределено Тогда
		СтрокаДанных = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока;
	КонецЕсли; 
	Если СтрокаДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		Объект = СтрокаДанных.Ссылка.ПолучитьОбъект();
		Если Объект = Неопределено Тогда
			Объект = Новый УдалениеОбъекта(СтрокаДанных.Ссылка);
		КонецЕсли; 
	ИначеЕсли мТекущаяГруппаТипаМетаданных = "Регистр" Тогда
		Объект = мМакетныйОбъект;
		// Антибаг 8.2.15 http://www.partners.v8.1c.ru/forum/thread.jsp?id=1034617#1034617
		//Объект.Отбор.Сбросить();
		Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
			Если Ложь
				Или Колонка.Имя = "НомерСообщения"
				Или Колонка.Имя = "Узел"
			Тогда
				Продолжить;
			КонецЕсли; 
			ЭлементОтбора = мМакетныйОбъект.Отбор[Колонка.Имя];
			ирОбщий.ПрисвоитьЕслиНеРавноЛкс(ЭлементОтбора.Использование, Истина);
			ЭлементОтбора.Значение = СтрокаДанных[Колонка.Имя];
		КонецЦикла;
		Объект.Прочитать();
	ИначеЕсли мТекущаяГруппаТипаМетаданных = "Константа" Тогда
		Объект = мМакетныйОбъект;
		Объект.Прочитать();
	КонецЕсли;
	Возврат Объект;
	
КонецФункции


// показываем результат стандарной выгрузки
Процедура КоманднаяПанельДереваОбъектовПоказатьРезультатСтандартнойВыгрузки(Кнопка)
	
	Если ЭлементыФормы.ТаблицаИзменений.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-16");
	Объект = ПолучитьОбъектДанныхПоСтрокеИзменений();
	Попытка
		ЗаписатьXML(ЗаписьXML, Объект); 
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	СтрXML = ЗаписьXML.Закрыть();
	ирОбщий.ОткрытьТекстЛкс(СтрXML, "XML представление объекта """ + ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока.ПолноеИмя + "." + Объект + """", "HTML", Истина, ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока);
	
КонецПроцедуры

Процедура ВыполнитьРегистрациюПервогоОбъектаДляУзла(Знач ПрефиксОбъектаМетаданных, Знач НазваниеОбъекта)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Первые 1
	               |	" + мВнутреннееИмяТаблицы + ".Ссылка
	               |ИЗ
	               |	" + ПрефиксОбъектаМетаданных + "." + НазваниеОбъекта + " КАК " + мВнутреннееИмяТаблицы;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(мВыбранныеУзлы, Выборка.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьРегистрациюРегистра(Знач ИмяТипаОбъектаМетаданных, Знач ПрефиксОбъектаМетаданных, Знач НазваниеОбъекта)
	
	КолонкаОтбора = "Регистратор";
	Если ИмяТипаОбъектаМетаданных = "РегистрыСведений" Тогда
		МетаданныеРегистра = Метаданные.РегистрыСведений[НазваниеОбъекта];
		Если МетаданныеРегистра.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			КолонкаОтбора = "";
			МассивКолонокОтбора = Новый Массив();
			Если МетаданныеРегистра.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
				МассивКолонокОтбора.Добавить("Период");		
			КонецЕсли;
			Для Каждого КолонкаИзмерений Из МетаданныеРегистра.Измерения Цикл
				Если КолонкаИзмерений.ОсновнойОтбор = Истина Тогда
					МассивКолонокОтбора.Добавить(КолонкаИзмерений.Имя);	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;		
	
	Запрос = Новый Запрос();
	СтрокаКолонокВыборкиДанных = "";
	Если КолонкаОтбора <> "" Тогда
		СтрокаКолонокВыборкиДанных = мВнутреннееИмяТаблицы + "." + КолонкаОтбора;
	Иначе
		Для Каждого ЭлементКолонки Из МассивКолонокОтбора Цикл
			СтрокаКолонокВыборкиДанных = СтрокаКолонокВыборкиДанных + "," + Символы.ПС + мВнутреннееИмяТаблицы + "." + ЭлементКолонки;
		КонецЦикла;
		СтрокаКолонокВыборкиДанных = СокрЛП(Сред(СтрокаКолонокВыборкиДанных, 2));
	КонецЕсли;
	
	// нет ни измерений, ни периодичности
	Если ПустаяСтрока(СтрокаКолонокВыборкиДанных) Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(мВыбранныеУзлы, МетаданныеРегистра);	
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ Первые 1
				   |	" + СтрокаКолонокВыборкиДанных + " 
				   |ИЗ
				   |	" + ПрефиксОбъектаМетаданных + "." + НазваниеОбъекта + " КАК " + мВнутреннееИмяТаблицы;
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ИмяТипаОбъектаМетаданных = "Последовательности" Тогда
			НаборЗаписей = Последовательности[НазваниеОбъекта].СоздатьНаборЗаписей();
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыСведений" Тогда
			НаборЗаписей = РегистрыСведений[НазваниеОбъекта].СоздатьНаборЗаписей();
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыНакопления" Тогда	
			НаборЗаписей = РегистрыНакопления[НазваниеОбъекта].СоздатьНаборЗаписей();
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыБухгалтерии" Тогда	
			НаборЗаписей = РегистрыБухгалтерии[НазваниеОбъекта].СоздатьНаборЗаписей();	
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыРасчета" Тогда	
			НаборЗаписей = РегистрыРасчета[НазваниеОбъекта].СоздатьНаборЗаписей();	
		КонецЕсли;
		Если КолонкаОтбора <> "" Тогда 
			НаборЗаписей.Отбор[КолонкаОтбора].Установить(Выборка[КолонкаОтбора]);
		Иначе
			Для Каждого ЭлементКолонки Из МассивКолонокОтбора Цикл
				НаборЗаписей.Отбор[ЭлементКолонки].Установить(Выборка[ЭлементКолонки]);
			КонецЦикла;
		КонецЕсли;
		ПланыОбмена.ЗарегистрироватьИзменения(мВыбранныеУзлы, НаборЗаписей);
	КонецЕсли;
	
КонецПроцедуры

Функция ВыбратьУзелОбменаПоМетаданным()
	
	ИмяПланаОбмена = мМетаданныеПланаОбмена.Имя;
	
	Запрос = Новый запрос();
	Запрос.Текст = "ВЫБРАТЬ первые 2
	               |	ПланОбменаРезультат.Ссылка
	               |ИЗ
	               |	ПланОбмена." + ИмяПланаОбмена + " КАК ПланОбменаРезультат
	               |ГДЕ
	               |	ПланОбменаРезультат.ПометкаУдаления = Ложь
	               |	И ПланОбменаРезультат.Ссылка <> &ТекущийУзелОбмена";
				   
	Запрос.УстановитьПараметр("ТекущийУзелОбмена", ПланыОбмена[ИмяПланаОбмена].ЭтотУзел());
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаРезультата.Количество() = 1 Тогда
		
		Возврат ТаблицаРезультата[0].Ссылка;
		
	Иначе
		
		Возврат ПланыОбмена[ИмяПланаОбмена].ПустаяСсылка();
		
	КонецЕсли;
	
КонецФункции

Процедура ПланОбменаПриИзменении(Элемент = Неопределено) Экспорт
	
	//Если Элемент <> Неопределено Тогда
	//	// Антибаг платформы 8.2.16 http://partners.v8.1c.ru/forum/thread.jsp?id=1077270#1077270
	//	Элемент.Значение = Элемент.Значение;
	//КонецЕсли; 
	Если Элемент = Неопределено Тогда
		Элемент = ЭлементыФормы.ИмяПланОбмена;
	КонецЕсли; 
	Узлы.Очистить();
	ТаблицаИзменений.Очистить();
	СоставПланаОбмена.Строки.Очистить();
	мМетаданныеПланаОбмена = Метаданные.ПланыОбмена.Найти(ИмяПланОбмена);
	мДанныеПоКоличествуИзменений = Неопределено;
	Если Не ЗначениеЗаполнено(ИмяПланОбмена) Тогда
		ПриИзмененииВыбранныхУзлов();
		Возврат;
	КонецЕсли; 
	ЭтаФорма.РИБ = мМетаданныеПланаОбмена.РаспределеннаяИнформационнаяБаза;
	КПУзлыОбновить(, Истина);
	КП_ТаблицаИзмененийОбновить();
	//ПроверитьЗаполнитьИтогиТаблицыПоУзлам();
	ЭлементыФормы.ТаблицаИзменений.ТолькоПросмотр = Истина;
	ирОбщий.ПолеВводаСИсториейВыбора_ПриИзмененииЛкс(Элемент, Метаданные().Имя);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЗначениеЗаполнено(ПараметрУзелОбмена) Тогда
		ИмяПланОбмена = ПараметрУзелОбмена.Метаданные().Имя;
		мВыбранныеУзлы.Добавить(ПараметрУзелОбмена);
	КонецЕсли; 
	ПланОбменаПриИзменении();
	Если ЗначениеЗаполнено(ПараметрУзелОбмена) Тогда
		СтрокаУзла = Узлы.Найти(ПараметрУзелОбмена, "Ссылка");
		Если СтрокаУзла <> Неопределено Тогда
			ЭлементыФормы.Узлы.ТекущаяСтрока = СтрокаУзла;
		КонецЕсли; 
		ПараметрУзелОбмена = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

Процедура УдалитьРегистрациюДляВсехОбъектовНажатие(Элемент)
	
	Если мВыбранныеУзлы.Количество() = 0 Тогда
		Предупреждение("Сначала необходимо выбрать узлы");
		Возврат;
	КонецЕсли; 
	РезультатРегистрации = ИзменитьРегистрациюИзмененийНаВыбранныхУзлахДляВсегоСоставаПланаОбмена(Ложь);
	Если РезультатРегистрации Тогда
	     ПостроитьДеревоСоставаПланаОбмена();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзмененияДляВсехОбъектовНажатие(Элемент)
	
	Если мВыбранныеУзлы.Количество() = 0 Тогда
		Предупреждение("Сначала необходимо выбрать узлы");
		Возврат;
	КонецЕсли; 
	РезультатРегистрации = ИзменитьРегистрациюИзмененийНаВыбранныхУзлахДляВсегоСоставаПланаОбмена(Истина);
	Если РезультатРегистрации Тогда
	     ПостроитьДеревоСоставаПланаОбмена();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзмененияПоОдномуОбъектуНажатие(Элемент)
	
	Если мВыбранныеУзлы.Количество() = 0 Тогда
		Предупреждение("Сначала необходимо выбрать узлы");
		Возврат;
	КонецЕсли; 
	ФормаВопроса = ПолучитьФорму("ФормаВыбораОбновленияРегистрации", ЭтаФорма);
	ФормаВопроса.ТекстВопросаПользователю = "Операция регистрации одного изменения для каждого типа объектов необратима, Вы уверены, что хотите зарегистрировать изменения?";
	РезультатОткрытия = ФормаВопроса.ОткрытьМодально();
	Если РезультатОткрытия <> Истина Тогда
		Возврат;
	КонецЕсли;
	мДанныеПоКоличествуИзменений = Неопределено;
	ТолькоДляОбъектовСАвтоматическойРегистрацией = ФормаВопроса.ОбновлятьТолькоДляЭлементовСАвтоРегистрацией;
	Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(СоставПланаОбмена.Строки.Количество(), "Классы метаданных");
	Для Каждого СтрокаДереваМетаданных Из СоставПланаОбмена.Строки Цикл
		ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
		Если СтрокаДереваМетаданных.ПолноеИмя = "Константы" Тогда
			Для Каждого СтрокаНазваний Из СтрокаДереваМетаданных.Строки Цикл
				Если Истина
					И ТолькоДляОбъектовСАвтоматическойРегистрацией
					И СтрокаНазваний.РежимАвтоРегистрации = Ложь 
				Тогда
					Продолжить;
				КонецЕсли;
				ПланыОбмена.ЗарегистрироватьИзменения(мВыбранныеУзлы, Константы[СтрокаНазваний.Имя]);
			КонецЦикла;
		ИначеЕсли ирОбщий.ЛиКорневойТипСсылочногоОбъектаБДЛкс(СтрокаДереваМетаданных.Имя) Тогда
			ИндикаторМД = ирОбщий.ПолучитьИндикаторПроцессаЛкс(СтрокаДереваМетаданных.Строки.Количество(), СтрокаДереваМетаданных.ПолноеИмя);
			Для Каждого СтрокаНазваний Из СтрокаДереваМетаданных.Строки Цикл
				ирОбщий.ОбработатьИндикаторЛкс(ИндикаторМД);
				Если Истина
					И ТолькоДляОбъектовСАвтоматическойРегистрацией
					И СтрокаНазваний.РежимАвтоРегистрации = Ложь 
				Тогда
					Продолжить;
				КонецЕсли;
				ВыполнитьРегистрациюПервогоОбъектаДляУзла(СтрокаДереваМетаданных.Имя, СтрокаНазваний.Имя);
			КонецЦикла;
			ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
		ИначеЕсли ирОбщий.ЛиКорневойТипРегистраБДЛкс(СтрокаДереваМетаданных.Имя) Тогда
			ИндикаторМД = ирОбщий.ПолучитьИндикаторПроцессаЛкс(СтрокаДереваМетаданных.Строки.Количество(), СтрокаДереваМетаданных.ПолноеИмя);
			Для Каждого СтрокаНазваний Из СтрокаДереваМетаданных.Строки Цикл
				ирОбщий.ОбработатьИндикаторЛкс(ИндикаторМД);
				Если Истина
					И ТолькоДляОбъектовСАвтоматическойРегистрацией
					И СтрокаНазваний.РежимАвтоРегистрации = Ложь 
				Тогда
					Продолжить;
				КонецЕсли;
				ОписаниеТипаМетаданных = мСоответствиеСтрокДереваИМетаданных[СтрокаДереваМетаданных.Имя].ОписаниеТипа;
				ВыполнитьРегистрациюРегистра(ОписаниеТипаМетаданных.Множественное, СтрокаДереваМетаданных.Имя, СтрокаНазваний.Имя);
			КонецЦикла;
			ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
		КонецЕсли;	
	КонецЦикла;
	ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
	ПостроитьДеревоСоставаПланаОбмена();
		
КонецПроцедуры

Процедура КнопкаИзменитьНомераНажатие(Элемент)
	
	Если ЭлементыФормы.Узлы.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ФормаИзмененияНомеров = ПолучитьФорму("ИзменениеНомеровСообщений", ЭтаФорма);
	ФормаИзмененияНомеров.УзелОбмена = ЭлементыФормы.Узлы.ТекущаяСтрока.Ссылка;
	ФормаИзмененияНомеров.ОткрытьМодально();
	
КонецПроцедуры

Процедура ДеревоОбменаПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ДеревоОбменаПриАктивизацииСтроки(Элемент = Неопределено)
	
	Если ЭтаФорма.ПоказыватьСодержимое Тогда
		КП_ТаблицаИзмененийОбновить();
	КонецЕсли; 
	
КонецПроцедуры

//Процедура НаборЗаписейВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
//	
//	ЗначениеКолонки = ВыбраннаяСтрока[Колонка.Имя];
//	глПроверитьОткрытьСсылку(ЗначениеКолонки);

//КонецПроцедуры

//Процедура НаборЗаписейПередНачаломИзменения(Элемент, Отказ)
//	
//	Отказ = Истина;
//	
//КонецПроцедуры

//Процедура КлючОбъектаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
//	
//	ЗначениеКолонки = ВыбраннаяСтрока[Колонка.Имя];
//	глПроверитьОткрытьСсылку(ЗначениеКолонки);

//КонецПроцедуры

Процедура ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзмененияПриИзменении(Элемент = Неопределено)
	
	Если Не ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения Тогда
		ЭтаФорма.ОтображатьТолькоМетаданныеСИзменениями = Ложь;
		мДанныеПоКоличествуИзменений = Неопределено;
	КонецЕсли; 
	ЭлементыФормы.ОтображатьТолькоМетаданныеСИзменениями.Доступность = ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения;
	ПостроитьДеревоСоставаПланаОбмена();
	
КонецПроцедуры

Процедура ОтображатьТолькоМетаданныеСИзменениямиПриИзменении(Элемент)
	
	ПостроитьДеревоСоставаПланаОбмена();

КонецПроцедуры

Процедура ТаблицаИзмененийВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Ложь
		Или Колонка.Имя = "НомерСообщения" 
		Или (Истина
			И Колонка.Имя = "Ссылка"
			И мТекущаяГруппаТипаМетаданных = "Ссылочный")
	Тогда
		КП_ТаблицаИзмененийОткрыть();
		СтандартнаяОбработка = Ложь;
		Возврат;
	Иначе
		КП_ТаблицаИзмененийОткрыть();
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли; 
	ирОбщий.ЯчейкаТабличногоПоляРасширенногоЗначения_ВыборЛкс(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийМенеджерТабличногоПоля(Кнопка)
	
	 ирОбщий.ПолучитьФормуЛкс("Обработка.ирМенеджерТабличногоПоля.Форма",, ЭтаФорма, ).УстановитьСвязь(ЭлементыФормы.ТаблицаИзменений);
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийОткрыть(Кнопка = Неопределено)
	
	ВыбраннаяСтрока = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПолноеИмяМД = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока.ПолноеИмя;
	Если Найти(ПолноеИмяМД, "Константа") = 1 Тогда
		//Предупреждение(Данные.Значение, , Строка(ПолноеИмя));
	Иначе
		Попытка
			Пустышка = Тип(СтрЗаменить(ПолноеИмяМД, ".", "Объект."));
			ЭтоСсылочныйОбъект = Истина;
		Исключение
			ЭтоСсылочныйОбъект = Ложь;
		КонецПопытки; 
		Если ЭтоСсылочныйОбъект Тогда
			ОткрытьЗначение(ВыбраннаяСтрока.Ссылка); // Ссылку надо брать, чтобы в управляемом режиме открывалась управляемая форма
		Иначе
			Отбор = Новый Структура;
			Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
				Если Ложь
					Или Колонка.Имя = "НомерСообщения"
					Или Колонка.Имя = "Узел"
				Тогда
					Продолжить;
				КонецЕсли; 
				Отбор.Вставить(Колонка.Имя, ВыбраннаяСтрока[Колонка.Имя]);
			КонецЦикла;
			ирОбщий.ОткрытьФормуСпискаСОтборомЛкс(ПолноеИмяМД, Отбор);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийРедакторОбъектаБДЯчейки(Кнопка)
	
	ирОбщий.ОткрытьСсылкуЯчейкиВРедактореОбъектаБДЛкс(ЭлементыФормы.ТаблицаИзменений);
	
КонецПроцедуры

Процедура КоманднаяПанельДереваОбъектовОткрытьФормуСписка(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Если ТекущаяСтрока.Уровень() = 1 Тогда
		Если ТекущаяСтрока.Родитель.Имя = "Константа" Тогда
			ирОбщий.ОткрытьКонстантуВСпискеЛкс(ТекущаяСтрока.Имя);
		ИначеЕсли ТекущаяСтрока.Родитель.Имя = "Перерасчет" Тогда
			Возврат;
		Иначе
			ОткрытьФорму(ТекущаяСтрока.ПолноеИмя + ".ФормаСписка");
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗагружатьИзмененияПриИзменении(Элемент)
	
	ПостроитьДеревоСоставаПланаОбмена();

КонецПроцедуры

Функция ПолучитьПостроительДляТаблицыИзменений(ВыбраннаяСтрока = Неопределено, МаксимальнаяПорция = 0)

	Если ВыбраннаяСтрока = Неопределено Тогда
		ВыбраннаяСтрока = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока;
	КонецЕсли; 
	ТекстВыбор = "ВЫРАЗИТЬ(_Т.Узел КАК ПланОбмена." + ИмяПланОбмена + ") КАК Узел, _Т.*";
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		ТекстВыбор = ТекстВыбор + ", ВЫБОР КОГДА ЕСТЬNULL(_Т.Ссылка.Ссылка, 0) = 0 ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ОбъектУдален, _Т.Ссылка.ПометкаУдаления КАК ПометкаУдаления";
	КонецЕсли; 
	ТекстПервые = "";
	Если МаксимальнаяПорция > 0 Тогда
		ТекстПервые = " ПЕРВЫЕ " + Формат(МаксимальнаяПорция, "ЧГ=");
	КонецЕсли; 
	Построитель = Новый ПостроительЗапроса("ВЫБРАТЬ " + ТекстПервые + ТекстВыбор + " ИЗ " + ВыбраннаяСтрока.ПолноеИмя + ".Изменения КАК _Т АВТОУПОРЯДОЧИВАНИЕ");
	Построитель.ЗаполнитьНастройки();
	Для Каждого ДоступноеПоле Из Построитель.ДоступныеПоля Цикл
		Если ДоступноеПоле.Имя = "НомерСообщения" Тогда
			Продолжить;
		КонецЕсли; 
		Построитель.Порядок.Добавить(ДоступноеПоле.Имя, ДоступноеПоле.Имя);
	КонецЦикла;
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		ДоступноеПолеДата = Построитель.ДоступныеПоля.Ссылка.Поля.Найти("Дата");
		Если ДоступноеПолеДата <> Неопределено Тогда
			Построитель.ВыбранныеПоля.Добавить(ДоступноеПолеДата.ПутьКДанным, "Дата");
		КонецЕсли;
	КонецЕсли; 
	Построитель.ВыбранныеПоля.Удалить(Построитель.ВыбранныеПоля.Найти("Узел1"));
	Если мВыбранныеУзлы.Количество() = 1 Тогда
		Построитель.ВыбранныеПоля.Удалить(Построитель.ВыбранныеПоля.Найти("Узел"));
	КонецЕсли; 
	ЭлементОтбора = Построитель.Отбор.Добавить("Узел");
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ВидСравнения = ВидСравнения.ВСписке;
	СписокУзлов = Новый СписокЗначений;
	Если ЭлементыФормы.ИтогиТаблицыПоУзлам.ТекущаяСтрока.Узел = "<Все>" Тогда
		СписокУзлов.ЗагрузитьЗначения(мВыбранныеУзлы);
	Иначе
		СписокУзлов.Добавить(ЭлементыФормы.ИтогиТаблицыПоУзлам.ТекущаяСтрока.Узел);
	КонецЕсли; 
	ЭлементОтбора.Значение = СписокУзлов;
	
	// Антибаг 8.2.14 http://partners.v8.1c.ru/forum/thread.jsp?id=1017264#1017264
	Если ирКэш.Получить().ВерсияПлатформы >= 802014 Тогда
		Если мТекущаяГруппаТипаМетаданных = "Регистр" Тогда
			ОбъектМетаданных = ирОбщий.НайтиОбъектМетаДанныхПоПолномуИмениТаблицыБДЛкс(ВыбраннаяСтрока.ПолноеИмя);
			Для Каждого ОбщийРеквизит Из Метаданные.ОбщиеРеквизиты Цикл
				ВыбранноеПоле = Построитель.ВыбранныеПоля.Найти(ОбщийРеквизит.Имя);
				Если Истина
					И ВыбранноеПоле <> Неопределено
					И ОбъектМетаданных.Измерения.Найти(ОбщийРеквизит.Имя) = Неопределено
				Тогда
					Если ирОбщий.ЛиОбщийРеквизитИспользуетсяВОбъектеМетаданныхЛкс(ОбщийРеквизит, ОбъектМетаданных) Тогда
						Построитель.ВыбранныеПоля.Удалить(ВыбранноеПоле);
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
	Возврат Построитель;

КонецФункции

Процедура КП_ТаблицаИзмененийОбновить(Кнопка = Неопределено)
	
	ОбновитьИтогиПоТаблице();
	СтруктураТекущейСтроки = Неопределено;
	ТекущаяКолонка = ЭлементыФормы.ТаблицаИзменений.ТекущаяКолонка;
	Если ТекущаяКолонка <> Неопределено Тогда
		ТекущаяКолонка = ТекущаяКолонка.Данные;
	КонецЕсли; 
	ТекущаяСтрокаИзменений = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока;
	Если ТекущаяСтрокаИзменений <> Неопределено Тогда
		СтруктураТекущейСтроки = Новый Структура();
		Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
			СтруктураТекущейСтроки.Вставить("Ссылка", ТекущаяСтрокаИзменений.Ссылка);
		Иначе
			Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
				Если Колонка.Имя = "НомерСообщения" Тогда
					Продолжить;
				КонецЕсли; 
				СтруктураТекущейСтроки.Вставить(Колонка.Имя, ТекущаяСтрокаИзменений[Колонка.Имя]);
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	ТаблицаИзменений.Очистить();
	ТаблицаИзменений.Колонки.Очистить();
	ВыбраннаяСтрока = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	УровеньВыбраннойСтроки = ВыбраннаяСтрока.Уровень();
	ЭлементыФормы.ТаблицаИзменений.ТолькоПросмотр = (УровеньВыбраннойСтроки = 0);
	Если УровеньВыбраннойСтроки = 1 Тогда
		ОбновитьИтогиПоТаблице();
		ирОбщий.ПолучитьМакетныйОбъектДанныхТаблицыБДЛкс(ВыбраннаяСтрока.ПолноеИмя, мМакетныйОбъект, мТекущаяГруппаТипаМетаданных);
		Построитель = ПолучитьПостроительДляТаблицыИзменений();
		//ЧислоСтрокДляЗагрузки = ирОбщий.КонтрольРазмераВыборкиПользователемЛкс(Построитель);
		//Если ЧислоСтрокДляЗагрузки > 0 Тогда
		//	Построитель = ПолучитьПостроительДляТаблицыИзменений(, ЧислоСтрокДляЗагрузки);
		//КонецЕсли;
		//// http://partners.v8.1c.ru/forum/thread.jsp?id=1034151#1034151
		////МаксимальныйРазмер = 500000;
		////Построитель = ПолучитьПостроительДляТаблицыИзменений(, МаксимальныйРазмер);
		
		Состояние("Выборка содержимого таблицы изменений...");
		мРезультатЗапроса = Построитель.Результат;
		ирОбщий.ЗагрузитьДинамическуюВыборкуВТабличноеПолеПервуюПорциюЛкс(ЭтаФорма, мРезультатЗапроса, мВыборкаРезультата,
			ЭлементыФормы.КП_ТаблицаИзменений);
		Состояние("Подготовка данных таблицы изменений...");
		// Дорогая операция!
		ТаблицаИзменений = ирОбщий.ПолучитьТаблицуСКолонкамиБезТипаNullЛкс(ТаблицаИзменений,,, "НомерСообщения");
		Состояние("");
		Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
			ТаблицаИзменений.Колонки.ОбъектУдален.Заголовок = "Объект удален";
			ТаблицаИзменений.Колонки.ПометкаУдаления.Заголовок = "Ссылка.Пометка удаления";
			Если ТаблицаИзменений.Колонки.Найти("Дата") <> Неопределено Тогда
				ТаблицаИзменений.Колонки.Дата.Заголовок = "Ссылка.Дата";
			КонецЕсли; 
		КонецЕсли; 
		ТаблицаИзменений.Колонки.НомерСообщения.Заголовок = "Номер сообщения";
	Иначе
		мРезультатЗапроса = Неопределено;
		мВыборкаРезультата = Неопределено;
		ОбновитьРазмерДинамическойТаблицы();
	КонецЕсли;
	ЭлементыФормы.ТаблицаИзменений.СоздатьКолонки();
	Для Каждого КолонкаТП Из ЭлементыФормы.ТаблицаИзменений.Колонки Цикл
		Если Ложь
			Или КолонкаТП.Имя = "НомерСообщения" 
			Или (Истина
				И мТекущаяГруппаТипаМетаданных = "Ссылочный"
				И КолонкаТП.Имя <> "Ссылка"
				И КолонкаТП.Имя <> "Узел")
		Тогда
			КолонкаТП.ТолькоПросмотр = Истина;
		Иначе
			КолонкаТП.ЭлементУправления.УстановитьДействие("ОкончаниеВводаТекста", Новый Действие("ПолеВводаКолонкиСсылка_ОкончаниеВводаТекста"));
		КонецЕсли;
		КолонкаТП.ТекстШапки = ТаблицаИзменений.Колонки[КолонкаТП.Имя].Заголовок;
	КонецЦикла;
	Если СтруктураТекущейСтроки <> Неопределено Тогда
		НоваяСтруктураТекущейСтроки = Новый Структура();
		Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
			Если Колонка.Имя = "НомерСообщения" Тогда
				Продолжить;
			КонецЕсли; 
			Если СтруктураТекущейСтроки.Свойство(Колонка.Имя) Тогда
				НоваяСтруктураТекущейСтроки.Вставить(Колонка.Имя, СтруктураТекущейСтроки[Колонка.Имя]);
			КонецЕсли; 
		КонецЦикла;
		НайденныеСтроки = ТаблицаИзменений.НайтиСтроки(НоваяСтруктураТекущейСтроки);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НоваяТекущаяСтрока = НайденныеСтроки[0];
			Попытка
				ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока = НоваяТекущаяСтрока;
			Исключение
				// Если вызывано из ПриОкончанииРедактирования строки
			КонецПопытки; 
		КонецЕсли; 
	КонецЕсли; 
	Если ТекущаяКолонка <> Неопределено Тогда
		НоваяТекущаяКолонка = ЭлементыФормы.ТаблицаИзменений.Колонки.Найти(ТекущаяКолонка);
		Если НоваяТекущаяКолонка <> Неопределено Тогда
			ЭлементыФормы.ТаблицаИзменений.ТекущаяКолонка = НоваяТекущаяКолонка;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ГлавнаяКоманднаяПанельНовоеОкно(Кнопка)
	
	ирОбщий.ОткрытьНовоеОкноОбработкиЛкс(ЭтотОбъект);
	
КонецПроцедуры

Процедура ДействияФормыОПодсистеме(Кнопка)
	
	ирОбщий.ОткрытьСправкуПоПодсистемеЛкс(ЭтотОбъект);
	
КонецПроцедуры

Процедура ТаблицаИзмененийПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ВыделенныеСтроки = ЭлементыФормы.ТаблицаИзменений.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Ответ = Вопрос("Вы действительно хотите удалить регистрацию по " + ВыделенныеСтроки.Количество() + " изменениям?", РежимДиалогаВопрос.ОКОтмена);
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	СтрокаДереваТекущихДанных = ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные;
	СтрокаОбновления = ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные;
	Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(ВыделенныеСтроки.Количество(), "Удаление регистрации");
	НачальноеКоличество = ВыделенныеСтроки.Количество(); 
	Для СчетчикВыделенныеСтроки = 1 По НачальноеКоличество Цикл
		ВыделеннаяСтрока = ВыделенныеСтроки[НачальноеКоличество - СчетчикВыделенныеСтроки];
		ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
		КлючОбъекта = ПолучитьКлючОбъектаСтроки(ВыделеннаяСтрока);
		ПланыОбмена.УдалитьРегистрациюИзменений(мВыбранныеУзлы, КлючОбъекта);
		ТаблицаИзменений.Удалить(ВыделеннаяСтрока);
	КонецЦикла;
	ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
	ОбновитьИтогиПоТаблице();
	//КП_ТаблицаИзмененийОбновить();

КонецПроцедуры

Процедура ТаблицаИзмененийПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТаблицаИзмененийПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	Объект = ПолучитьОбъектДанныхПоСтрокеИзменений();
	лУзел = Неопределено;
	Если ТаблицаИзменений.Колонки.Найти("Узел") <> Неопределено Тогда
		лУзел = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока.Узел;
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(лУзел) Тогда
		лУзел = мВыбранныеУзлы;
	КонецЕсли; 
	ПланыОбмена.ЗарегистрироватьИзменения(лУзел, Объект);
	//ОбновитьИтогиПоТаблице();
	КП_ТаблицаИзмененийОбновить();
	
КонецПроцедуры

Процедура ТаблицаИзмененийПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ирОбщий.ТабличноеПолеПриВыводеСтрокиЛкс(Элемент, ОформлениеСтроки, ДанныеСтроки, ЭлементыФормы.КП_ТаблицаИзменений.Кнопки.Пустые);
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийПустые(Кнопка)
	
	ирОбщий.КнопкаОтображатьПустыеИИдентификаторыНажатиеЛкс(Кнопка);
	ЭлементыФормы.ТаблицаИзменений.ОбновитьСтроки();
	
КонецПроцедуры

Процедура ТаблицаИзмененийПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если Не ОтменаРедактирования Тогда
		Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
			Если Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.Ссылка) Тогда
				Отказ = Истина;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПолеВводаКолонкиСсылка_ОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ирОбщий.ПолеВвода_ОкончаниеВводаТекстаЛкс(Элемент, Текст, Значение, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПослеВосстановленияЗначений()
	
	ПланОбменаПриИзменении();

КонецПроцедуры

Процедура КП_ТаблицаИзмененийКонсольКомпоновки(Кнопка)
	
	Если ЭлементыФормы.ТаблицаИзменений.Колонки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	Построитель = ПолучитьПостроительДляТаблицыИзменений();
	// Из-за бага платформы пока так не стоит делать
	//Построитель.Текст = СтрЗаменить(Построитель.Текст, "АВТОУПОРЯДОЧИВАНИЕ", "");
	//Отчеты.ирКонсольКомпоновокДанных.Создать().ОткрытьПоЗапросу(, Новый Структура("Узел", УзелОбмена));
	Запрос = Построитель.ПолучитьЗапрос();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "АВТОУПОРЯДОЧИВАНИЕ", "");
	КонсольКомпоновокДанных = ирОбщий.ПолучитьОбъектПоПолномуИмениМетаданныхЛкс("Отчет.ирКонсольКомпоновокДанных");
	#Если _ Тогда
		КонсольКомпоновокДанных = Отчеты.ирКонсольКомпоновокДанных.Создать();
	#КонецЕсли
    КонсольКомпоновокДанных.ОткрытьПоЗапросу(Запрос);
	
	//Отчеты.ирКонсольКомпоновокДанных.Создать().ОткрытьПоОбъектуМетаданных(ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные.ПолноеИмя + ".Изменения", Новый Структура("Узел", УзелОбмена));
	
КонецПроцедуры

Процедура ПланОбменаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ирОбщий.ПолеВводаСИсториейВыбора_НачалоВыбораИзСпискаЛкс(Элемент, Метаданные().Имя);

КонецПроцедуры

Процедура КП_ТаблицаИзмененийЗагрузитьПолностью(Кнопка)
	
	ирОбщий.ЗагрузитьДинамическуюВыборкуВТабличноеПолеПолностьюЛкс(ЭтаФорма, мВыборкаРезультата, ЭлементыФормы.КП_ТаблицаИзменений);
	
КонецПроцедуры

Процедура ОбновитьРазмерДинамическойТаблицы() Экспорт

	ирОбщий.ПослеЗагрузкиДинамическойВыборкиВТабличноеПолеЛкс(ЭтаФорма, мВыборкаРезультата,
		ЭлементыФормы.КП_ТаблицаИзменений, ЭлементыФормы.КоличествоСтрокИзменений);

КонецПроцедуры // ОбновитьРазмерТаблицы()

Процедура СтруктураКоманднойПанелиНажатие(Кнопка)
	
	ирОбщий.ОткрытьСтруктуруКоманднойПанелиЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийСброситьНомер(Кнопка)
	
	Отказ = Истина;
	ВыделенныеСтроки = ЭлементыФормы.ТаблицаИзменений.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Ответ = Вопрос("Вы действительно хотите сбросить номера сообщений по " + ВыделенныеСтроки.Количество() + " объектам?", РежимДиалогаВопрос.ОКОтмена);
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	СтрокаДереваТекущихДанных = ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные;
	СтрокаОбновления = ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные;
	Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(ВыделенныеСтроки.Количество(), "Сброс номеров сообщений");
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
		КлючОбъекта = ПолучитьКлючОбъектаСтроки(ВыделеннаяСтрока);
		ПланыОбмена.ЗарегистрироватьИзменения(мВыбранныеУзлы, КлючОбъекта);
		ВыделеннаяСтрока.НомерСообщения = Null;
	КонецЦикла;
	ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
	
КонецПроцедуры

Функция ПолучитьКлючОбъектаСтроки(ВыделеннаяСтрока)

	КлючОбъекта = Неопределено;
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		КлючОбъекта = ВыделеннаяСтрока.Ссылка;
	ИначеЕсли мТекущаяГруппаТипаМетаданных = "Регистр" Тогда
		КлючОбъекта = мМакетныйОбъект;
		// http://partners.v8.1c.ru/forum/thread.jsp?id=1034617#1034617
		//мМакетныйОбъект.Отбор.Сбросить();
		Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
			Если Ложь
				Или Колонка.Имя = "НомерСообщения" 
				Или Колонка.Имя = "Узел"
			Тогда
				Продолжить;
			КонецЕсли; 
			ЭлементОтбора = мМакетныйОбъект.Отбор[Колонка.Имя];
			ЭлементОтбора.Значение = ВыделеннаяСтрока[Колонка.Имя];
			ЭлементОтбора.Использование = Истина;
		КонецЦикла;
	ИначеЕсли мТекущаяГруппаТипаМетаданных = "Константа" Тогда
		КлючОбъекта = мМакетныйОбъект;
	КонецЕсли; 
	Возврат КлючОбъекта;

КонецФункции

Процедура КП_ТаблицаИзмененийРедакторОбъектаБДСтроки(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	КлючОбъекта = ПолучитьКлючОбъектаСтроки(ТекущаяСтрока);
	ирОбщий.ОткрытьСсылкуВРедактореОбъектаБДЛкс(КлючОбъекта);
	
КонецПроцедуры

Процедура ПланОбменаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Форма = ирКэш.Получить().ПолучитьФорму("ВыборОбъектаМетаданных", Элемент, ЭтаФорма);
	ДоступныеОбъектыМД = Новый Массив();
	ДоступныеОбъектыМД.Добавить("ПланОбмена." + ИмяПланОбмена); // Первый автоматически станет текущим
	Для Каждого МетаПланОбмена Из Метаданные.ПланыОбмена Цикл
		ДоступныеОбъектыМД.Добавить(МетаПланОбмена.ПолноеИмя());
	КонецЦикла;
	Форма.НачальноеЗначениеВыбора = ДоступныеОбъектыМД;
	СтандартнаяОбработка = Ложь;
	Форма.ОткрытьМодально();
	
КонецПроцедуры

Процедура ИмяПланОбменаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		лПолноеИмяОбъекта = Неопределено;
		Если ВыбранноеЗначение.Свойство("ПолноеИмяОбъекта", лПолноеИмяОбъекта) Тогда
			ЭтаФорма.ИмяПланОбмена = ирОбщий.ПолучитьПоследнийФрагментЛкс(лПолноеИмяОбъекта);
			ПланОбменаПриИзменении(Элемент);
		КонецЕсли;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	
КонецПроцедуры

Процедура УзлыПриИзмененииФлажка(Элемент, Колонка)
	
	ПриИзмененииВыбранныхУзлов();

КонецПроцедуры

Процедура ИмяПланОбменаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(ИмяПланОбмена) Тогда
		ОткрытьФорму("ПланОбмена." + ИмяПланОбмена + ".ФормаСписка");
	КонецЕсли; 
	
КонецПроцедуры

Процедура УзлыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ОткрытьЗначение(ВыбраннаяСтрока.Ссылка);
	
КонецПроцедуры

Процедура КПУзлыОбновить(Кнопка = Неопределено, ПринудительныйПризнакИзменениеВыбранныхУзлов = Неопределено)
	
	мДанныеПоКоличествуИзменений = Неопределено;
	Узлы.Очистить();
	//ИтогиТаблицыПоУзлам.Очистить();
	Если Не ЗначениеЗаполнено(ИмяПланОбмена) Тогда
		Возврат;
	КонецЕсли;
	ВыборкаУзлов = ПланыОбмена[ИмяПланОбмена].Выбрать();
	#Если _ Тогда
	    ВыборкаУзлов = ПланыОбмена.ПолныйИис.Выбрать();
	#КонецЕсли
	Пока ВыборкаУзлов.Следующий() Цикл
		СтрокаУзла = Узлы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаУзла, ВыборкаУзлов);
		Если мВыбранныеУзлы.Найти(ВыборкаУзлов.Ссылка) <> Неопределено Тогда
			СтрокаУзла.Пометка = Истина;
		КонецЕсли; 
		СтрокаУзла.СобственныйУзел = ПланыОбмена[ИмяПланОбмена].ЭтотУзел() = ВыборкаУзлов.Ссылка;
	КонецЦикла;
	НачальноеКоличество = мВыбранныеУзлы.Количество(); 
	ВыбранныеУзлыИзменены = Ложь;
	Для СчетчикмВыбранныеУзлы = 1 По НачальноеКоличество Цикл
		ВыбранныйУзел = мВыбранныеУзлы[НачальноеКоличество - СчетчикмВыбранныеУзлы];
		Если Узлы.Найти(ВыбранныйУзел, "Ссылка") = Неопределено Тогда
			Если ТипЗнч(ВыбранныйУзел) = Тип("ПланОбменаСсылка." + ИмяПланОбмена) Тогда
				НоваяСтрока = Узлы.Добавить();
				НоваяСтрока.Ссылка = ВыбранныйУзел;
				НоваяСтрока.Пометка = Истина;
			Иначе
				мВыбранныеУзлы.Удалить(НачальноеКоличество - СчетчикмВыбранныеУзлы);
				ВыбранныеУзлыИзменены = Истина;
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла;
	Если Ложь
		Или ПринудительныйПризнакИзменениеВыбранныхУзлов = Истина 
		Или ВыбранныеУзлыИзменены
		Или ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения // Это нужно, чтобы несуществующие узлы обновлялись
	Тогда
		ПриИзмененииВыбранныхУзлов();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриИзмененииВыбранныхУзлов(Групповое = Ложь)
	
	ВыбранныеСтроки = Узлы.НайтиСтроки(Новый Структура("Пометка", Истина));
	ТекстУзлов = "";
	мВыбранныеУзлы = Новый Массив();
	Для Каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
		Если ТекстУзлов <> "" Тогда
			ТекстУзлов = ТекстУзлов + ", ";
		КонецЕсли; 
		ТекстУзлов = ТекстУзлов + СокрЛП(ВыбраннаяСтрока.Код);
		мВыбранныеУзлы.Добавить(ВыбраннаяСтрока.Ссылка);
	КонецЦикла;
	ИтогиТаблицыПоУзлам.Очистить();
	СтрокаИтоговТаблицы = ИтогиТаблицыПоУзлам.Добавить();
	СтрокаИтоговТаблицы.Узел = "<Все>";
	ЭлементыФормы.ИтогиТаблицыПоУзлам.ТекущаяСтрока = СтрокаИтоговТаблицы;
	Для Каждого ВыбранныйУзел Из мВыбранныеУзлы Цикл
		СтрокаИтоговТаблицы = ИтогиТаблицыПоУзлам.Добавить();
		СтрокаИтоговТаблицы.Узел = ВыбранныйУзел;
	КонецЦикла;
	ЭтаФорма.ПредставлениеВыбранныхУзлов = "Выбрано " + мВыбранныеУзлы.Количество() + " узлов";
	Если ЗначениеЗаполнено(ТекстУзлов) Тогда
		ЭтаФорма.ПредставлениеВыбранныхУзлов = ЭтаФорма.ПредставлениеВыбранныхУзлов + ": " + ТекстУзлов;
	КонецЕсли;
	Если Ложь
		Или ОтображатьТолькоМетаданныеСИзменениями 
		Или ОтображатьТолькоТаблицыСАвторегистрацией 
		Или СоставПланаОбмена.Строки.Количество() = 0
	Тогда
		ПостроитьДеревоСоставаПланаОбмена();
	Иначе
		ОбновитьСтатистикуВСоставеПланаОбмена();
	КонецЕсли; 
	
КонецПроцедуры

Процедура КПУзлыУстановитьПометки(Кнопка)
	
	Если Узлы.Количество() > 0 Тогда
		СтараяПометка = Узлы.Найти(Истина, "СобственныйУзел").Пометка;
	КонецЕсли; 
	ирОбщий.ИзменитьПометкиВыделенныхСтрокЛкс(ЭлементыФормы.Узлы, , Истина);
	Если Узлы.Количество() > 0 Тогда
		Узлы.Найти(Истина, "СобственныйУзел").Пометка = СтараяПометка;
	КонецЕсли; 
	ПриИзмененииВыбранныхУзлов();
	
КонецПроцедуры

Процедура КПУзлыСнятьПометки(Кнопка)
	
	ирОбщий.ИзменитьПометкиВыделенныхСтрокЛкс(ЭлементыФормы.Узлы, , Ложь);
	ПриИзмененииВыбранныхУзлов();

КонецПроцедуры

Процедура ИтогиТаблицыПоУзламПриАктивизацииСтроки(Элемент)
	
	КП_ТаблицаИзмененийОбновить();
	
КонецПроцедуры

Процедура ОбновитьИтогиПоТаблице(ВыбраннаяСтрока = Неопределено)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		ВыбраннаяСтрока = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока;
	КонецЕсли; 
	Для Каждого СтрокаУзла Из ИтогиТаблицыПоУзлам Цикл
		СтрокаУзла.КоличествоЗарегистрированных = 0;
		СтрокаУзла.КоличествоНевыгруженных = 0;
		СтрокаУзла.КоличествоВыгруженных = 0;
	КонецЦикла; 
	Если Истина
		И ВыбраннаяСтрока <> Неопределено
		И ВыбраннаяСтрока.Уровень() = 1
		И мДанныеПоКоличествуИзменений <> Неопределено 
	Тогда
		ирОбщий.ОбновитьСтатистикуПоТаблицеОбъектаМДВРезультатеПакетаЛкс(мДанныеПоКоличествуИзменений, ВыбраннаяСтрока.ПолноеИмя, "ПолноеИмя", "КоличествоЗарегистрированных", Истина, 
				Новый Структура("Узел", Узлы.ВыгрузитьКолонку("Ссылка")));
		Для Каждого ТаблицаРезультата Из мДанныеПоКоличествуИзменений Цикл
			Для Каждого СтрокаРезультата Из ТаблицаРезультата.НайтиСтроки(Новый Структура("ПолноеИмя", ВыбраннаяСтрока.ПолноеИмя)) Цикл
				СтрокаУзла = ИтогиТаблицыПоУзлам.Найти(СтрокаРезультата.Узел, "Узел");
				Если СтрокаУзла = Неопределено Тогда
					Продолжить;
				КонецЕсли; 
				СтрокаУзла.КоличествоЗарегистрированных = СтрокаУзла.КоличествоЗарегистрированных + СтрокаРезультата.КоличествоЗарегистрированных;
				СтрокаУзла.КоличествоНевыгруженных = СтрокаУзла.КоличествоНевыгруженных + СтрокаРезультата.КоличествоНевыгруженных;
				СтрокаУзла.КоличествоВыгруженных = СтрокаУзла.КоличествоВыгруженных + СтрокаРезультата.КоличествоВыгруженных;
				СтрокаУзла = ИтогиТаблицыПоУзлам.Найти("<Все>", "Узел");
				СтрокаУзла.КоличествоЗарегистрированных = СтрокаУзла.КоличествоЗарегистрированных + СтрокаРезультата.КоличествоЗарегистрированных;
				СтрокаУзла.КоличествоНевыгруженных = СтрокаУзла.КоличествоНевыгруженных + СтрокаРезультата.КоличествоНевыгруженных;
				СтрокаУзла.КоличествоВыгруженных = СтрокаУзла.КоличествоВыгруженных + СтрокаРезультата.КоличествоВыгруженных;
			КонецЦикла;
		КонецЦикла; 
		ОбновитьКоличествоДляСтрокиДерева(ВыбраннаяСтрока);
	КонецЕсли;
	ОбновитьИтогиПоУзлам();

КонецПроцедуры

Процедура ТаблицаИзмененийПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ЭлементыФормы.ИтогиТаблицыПоУзлам.ТекущаяСтрока <> Неопределено Тогда
		лУзел = ЭлементыФормы.ИтогиТаблицыПоУзлам.ТекущаяСтрока.Узел;
		Если ТипЗнч(лУзел) <> Тип("Строка") Тогда
			Если Элемент.Колонки.Найти("Узел") <> Неопределено Тогда
				Элемент.ТекущиеДанные.Узел = лУзел;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура КоманднаяПанельДереваОбъектовОткрытьОбработкуОбъектов(Кнопка)
	
	Если ЭлементыФормы.СоставПланаОбмена.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ОбработкаОбъектов = ирОбщий.ПолучитьОбъектПоПолномуИмениМетаданныхЛкс("Обработка.ирПодборИОбработкаОбъектов");
	#Если _ Тогда
	    ОбработкаОбъектов = Обработки.ирПодборИОбработкаОбъектов.Создать();
	#КонецЕсли
	ФормаОбработки = ОбработкаОбъектов.ПолучитьФорму();
	ФормаОбработки.Открыть();
	ОбработкаОбъектов.ИспользоватьОтборПоУзлу = Истина;
	Если ЭлементыФормы.Узлы.ТекущаяСтрока <> Неопределено Тогда
		ОбработкаОбъектов.УзелОтбораОбъектов = ЭлементыФормы.Узлы.ТекущаяСтрока.Ссылка;
	Иначе
		ОбработкаОбъектов.УзелОтбораОбъектов = Новый ("ПланОбменаСсылка." + ИмяПланОбмена);
	КонецЕсли; 
	ПолныеИменаТаблиц = ПолучитьПолныеИменаВыделенныхТаблиц();
	Если ПолныеИменаТаблиц.Количество() = 1 Тогда
		ОбластьПоиска = ПолныеИменаТаблиц[0].Значение;
	Иначе
		ОбластьПоиска = ПолныеИменаТаблиц;
	КонецЕсли; 
	ФормаОбработки.УстановитьОбластьПоиска(ОбластьПоиска);
	
КонецПроцедуры

Процедура КПУзлыВыгрузкаДанныхЧерезФайл(Кнопка)
	
	Если ЭлементыФормы.Узлы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ОбработкаОбъектов = ирОбщий.ПолучитьОбъектПоПолномуИмениМетаданныхЛкс("Обработка.ирВыгрузкаЗагрузкаДанныхЧерезФайл");
	#Если _ Тогда
	    ОбработкаОбъектов = Обработки.ирВыгрузкаЗагрузкаДанныхЧерезФайл.Создать();
	#КонецЕсли
	ФормаОбработки = ОбработкаОбъектов.ПолучитьФорму();
	ФормаОбработки.Открыть();
	ОбработкаОбъектов.УзелВыборкиДанных = ЭлементыФормы.Узлы.ТекущаяСтрока.Ссылка;
	
КонецПроцедуры

Процедура КоманднаяПанельДереваОбъектовСверткаСодержимое(Кнопка)
	
	ИзменитьСвернутостьПанельТекущейТаблицы(Не ПоказыватьСодержимое);
	Если ПоказыватьСодержимое Тогда
		КП_ТаблицаИзмененийОбновить();
	КонецЕсли; 

КонецПроцедуры

Процедура ИзменитьСвернутостьПанельТекущейТаблицы(Видимость)
	
	ирОбщий.ИзменитьСвернутостьЛкс(Видимость, ЭлементыФормы.ПанельТекущейТаблицы, ЭтаФорма.ЭлементыФормы.РазделительГоризонтальный, ЭтаФорма.Панель, "низ");
	ЭлементыФормы.КоманднаяПанельДереваОбъектов.Кнопки.СверткаСодержимое.Пометка = Видимость;
	ЭтаФорма.ПоказыватьСодержимое = Видимость;

КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не Отказ Тогда
		ИзменитьСвернутостьПанельТекущейТаблицы(Истина);
	КонецЕсли; 

КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ирОбщий.ФормаОбработкаОповещенияЛкс(ЭтаФорма, ИмяСобытия, Параметр, Источник); 

КонецПроцедуры

Процедура КП_ТаблицаИзмененийКонсольОбработки(Кнопка)
	
	Ответ = Вопрос("Использовать значения текущей колонки?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ирОбщий.ОткрытьОбъектыИзВыделенныхЯчеекВПодбореИОбработкеОбъектовЛкс(ЭлементыФормы.ТаблицаИзменений);
	Иначе
		ирОбщий.ОткрытьОбъектыИзВыделенныхСтрокВПодбореИОбработкеОбъектовЛкс(ЭлементыФормы.ТаблицаИзменений, ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока.ПолноеИмя);
	КонецЕсли;

КонецПроцедуры

Процедура ОсновныеДействияФормыКопироватьРегистрациюВсехОбъектов(Кнопка)
	
	Если ЭлементыФормы.Узлы.ТекущаяСтрока = Неопределено Тогда
		Предупреждение("Сначала необходимо выбрать узел");
		Возврат;
	КонецЕсли; 
	ФормаВыбора = ПолучитьФорму("ВыборУзловПриемниковДляКопирования", ЭтаФорма);
	ФормаВыбора.Узлы = Узлы.Скопировать(Новый Структура("СобственныйУзел", Ложь));
	ФормаВыбора.УзелИсточник = ЭлементыФормы.Узлы.ТекущаяСтрока.Ссылка;
	РезультатФормы = ФормаВыбора.ОткрытьМодально();
	Если РезультатФормы <> Неопределено Тогда
		мДанныеПоКоличествуИзменений = Неопределено;
		ПостроитьДеревоСоставаПланаОбмена();
	КонецЕсли; 
	
КонецПроцедуры

Процедура КПУзлыРедактироватьОбъект(Кнопка)
	
	Если ЭлементыФормы.Узлы.ТекущаяСтрока <> Неопределено Тогда
		ирОбщий.ОткрытьСсылкуВРедактореОбъектаБДЛкс(ЭлементыФормы.Узлы.ТекущаяСтрока.Ссылка);
	КонецЕсли; 
	
КонецПроцедуры

Процедура КоманднаяПанельДереваОбъектовКопироватьРегистрациюОбъектов(Кнопка)
	
	Если ЭлементыФормы.Узлы.ТекущаяСтрока = Неопределено Тогда
		Предупреждение("Сначала необходимо выбрать узел");
		Возврат;
	КонецЕсли; 
	ПолныеИменаТаблиц = ПолучитьПолныеИменаВыделенныхТаблиц();
	Если ПолныеИменаТаблиц.Количество() = 0 Тогда
		Предупреждение("Сначала необходимо типы");
		Возврат;
	КонецЕсли; 
	ФормаВыбора = ПолучитьФорму("ВыборУзловПриемниковДляКопирования", ЭтаФорма);
	ФормаВыбора.Узлы = Узлы;
	ФормаВыбора.УзелИсточник = ЭлементыФормы.Узлы.ТекущаяСтрока.Ссылка;
	ОбъектыМД = Новый Массив;
	Для Каждого ЭлементСписка Из ПолныеИменаТаблиц Цикл
		ОбъектыМД.Добавить(ирОбщий.НайтиОбъектМетаДанныхПоПолномуИмениТаблицыБДЛкс(ЭлементСписка.Значение));
	КонецЦикла;
	ФормаВыбора.ОбъектыМД = ОбъектыМД;
	РезультатФормы = ФормаВыбора.ОткрытьМодально();
	Если РезультатФормы <> Неопределено Тогда
		мДанныеПоКоличествуИзменений = Неопределено;
		ПостроитьДеревоСоставаПланаОбмена();
	КонецЕсли; 
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийПодборВТаблицуИзменений(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока;
	Если ТекущаяСтрока.Родитель.Имя = "Константа" Тогда
		//ирОбщий.ОткрытьКонстантуВСпискеЛкс(ТекущаяСтрока.Имя);
	ИначеЕсли ТекущаяСтрока.Родитель.Имя = "Перерасчет" Тогда
		Возврат;
	Иначе
		ОписаниеТипов = Новый ОписаниеТипов(ирОбщий.БыстрыйМассивЛкс(ТипЗнч(мМакетныйОбъект)));
		Если Истина
			И ирОбщий.ЛиКорневойТипСсылкиЛкс(ТекущаяСтрока.Родитель.Имя)
			И ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока <> Неопределено 
		Тогда
			КлючОбъекта = ПолучитьКлючОбъектаСтроки(ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока);
		Иначе
			КлючОбъекта = ирОбщий.ПолучитьКлючСтрокиТаблицыБДИзСтрокиТаблицыЗначенийЛкс(ЭлементыФормы.СоставПланаОбмена.ТекущаяСтрока.ПолноеИмя, ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока, Истина);
		КонецЕсли; 
		ирОбщий.ОткрытьПодборСВыборомТипаЛкс(ЭлементыФормы.ТаблицаИзменений, ОписаниеТипов, КлючОбъекта);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ТаблицаИзмененийОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") Тогда
		Массив = ирОбщий.БыстрыйМассивЛкс(ВыбранноеЗначение);
	Иначе
		Массив = ВыбранноеЗначение;
	КонецЕсли; 
	Для Каждого ВыбранныйКлюч Из Массив Цикл
		ОбъектРегистрации = ирОбщий.ПолучитьОбъектДанныхИзСтрокиРезультатаЗапросаЛкс(ВыбранныйКлюч, мМакетныйОбъект, мТекущаяГруппаТипаМетаданных, Ложь);
		ПланыОбмена.ЗарегистрироватьИзменения(мВыбранныеУзлы, ОбъектРегистрации);
	КонецЦикла;
	КП_ТаблицаИзмененийОбновить();
	
КонецПроцедуры

Процедура КлсУниверсальнаяКомандаНажатие(Кнопка) Экспорт 
	
	ирОбщий.УниверсальнаяКомандаФормыЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура ОбработчикОжиданияСПараметрамиЛкс() Экспорт 
	
	ирОбщий.ОбработчикОжиданияСПараметрамиЛкс();

КонецПроцедуры

Процедура ОтображатьТолькоТаблицыСАвторегистрациейПриИзменении(Элемент)
	
	ПостроитьДеревоСоставаПланаОбмена();
	
КонецПроцедуры

Процедура УзлыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.СобственныйУзел Тогда
		ОформлениеСтроки.Ячейки.Пометка.ТолькоПросмотр = Истина;
		ОформлениеСтроки.ЦветФона = WebЦвета.Перламутровый;
		//ЖирныйШрифт = Новый Шрифт(,, Истина);
		//ОформлениеСтроки.Шрифт = ЖирныйШрифт;
	КонецЕсли; 
	
КонецПроцедуры

Процедура КПУзлыОткрытьОбработкуОбъектов(Кнопка)
	
	ирОбщий.ОткрытьОбъектыИзВыделенныхЯчеекВПодбореИОбработкеОбъектовЛкс(ЭлементыФормы.Узлы, "Ссылка");
	
КонецПроцедуры

ирОбщий.ИнициализироватьФормуЛкс(ЭтаФорма, "Обработка.ирРедакторИзмененийПоПлануОбмена.Форма.Форма");

СоставПланаОбмена.Колонки.Добавить("ПолноеИмя");
мСоответствиеСтрокДереваИМетаданных = Новый Соответствие;
ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения = Истина;
ЭлементыФормы.ОтображатьТолькоМетаданныеСИзменениями.Доступность = ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения;
ОтображатьТолькоМетаданныеСИзменениями = Истина;
мВыбранныеУзлы = Новый Массив();
ПоказыватьСодержимое = Истина;
мВнутреннееИмяТаблицы = "Таблица34582";
